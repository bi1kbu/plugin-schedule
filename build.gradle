plugins {
    id 'java'
    id "io.freefair.lombok" version "8.13"
    id "run.halo.plugin.devtools" version "0.6.1"
}

group 'com.bi1kbu.pluginschedule'

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('run.halo.tools.platform:plugin:2.22.0')
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

tasks.register('processUiResources', Copy) {
    from project(':ui').layout.buildDirectory.dir('dist')
    into layout.buildDirectory.dir('resources/main/console')
    dependsOn project(':ui').tasks.named('assemble')
    shouldRunAfter tasks.named('processResources')
}

tasks.named('classes') {
    dependsOn tasks.named('processUiResources')
}

tasks.register('bumpPatchVersion') {
    group = 'build'
    description = 'Increment patch version in gradle.properties (x.y.z -> x.y.(z+1)).'
    doLast {
        def versionFile = file('gradle.properties')
        if (!versionFile.exists()) {
            throw new GradleException("gradle.properties not found: ${versionFile.absolutePath}")
        }

        def content = versionFile.getText('UTF-8')
        def matcher = content =~ /(?m)^version\s*=\s*(\d+)\.(\d+)\.(\d+)\s*$/
        if (!matcher.find()) {
            throw new GradleException("Cannot parse version from gradle.properties, expected format: version=x.y.z")
        }

        def major = matcher.group(1) as int
        def minor = matcher.group(2) as int
        def patch = matcher.group(3) as int
        def newVersion = "${major}.${minor}.${patch + 1}".toString()

        def updated = content.replaceFirst(/(?m)^version\s*=\s*\d+\.\d+\.\d+\s*$/, "version=${newVersion}")
        versionFile.write(updated, 'UTF-8')
        project.version = newVersion.toString()
        logger.lifecycle("Version bumped: ${major}.${minor}.${patch} -> ${newVersion}")
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    doFirst {
        archiveVersion.set(project.version.toString())
    }
}

tasks.register('packagePlugin') {
    group = 'build'
    description = 'Bump patch version, then build plugin with the new version.'
    dependsOn tasks.named('bumpPatchVersion')
    dependsOn tasks.named('build')
}

tasks.named('build') {
    mustRunAfter tasks.named('bumpPatchVersion')
}

halo {
    version = '2.22'
}
